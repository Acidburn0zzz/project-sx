# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Que Quotion <quequotion@bugmenot.com>

pkgname=indicator-sensors
pkgver=0.8.r32.g6bfed17
pkgrel=2
pkgdesc="Small application to provide hardware sensor readings using the AppIndicator framework (git version)"
arch=('i686' 'x86_64')
url="https://github.com/alexmurray/indicator-sensors"
license=('GPL3')
provides=('indicator-sensors-git')
conflicts=('indicator-sensors-git')
depends=('dconf' 'libappindicator-gtk3' 'libatasmart' 'libnotify' 'libpeas' 'lm_sensors' 'sensor-icons' 'udisks2')
makedepends=('git' 'gnome-common' 'gobject-introspection' 'intltool')
optdepends=('libxnvctrl: NVIDIA GPU support')
source=("git+https://github.com/alexmurray/indicator-sensors"
        'is-udisks2.rules')
md5sums=('SKIP'
         '73468b5a1034ed57cb94c48ba26d2300')

pkgver() {
  cd "indicator-sensors"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd indicator-sensors
  sh ./autogen.sh
  ./configure --disable-schemas-compile --prefix=/usr
  make
}

package() {
  cd indicator-sensors
  make DESTDIR="$pkgdir" install
  mkdir -p -m 750 "$pkgdir"/etc/polkit-1/rules.d/
  install -m 644 ../is-udisks2.rules "$pkgdir"/etc/polkit-1/rules.d/

  #rm appicon provided provided by separate pkg
  rm $pkgdir/usr/share/icons/hicolor/48x48/apps/indicator-sensors.png
}

